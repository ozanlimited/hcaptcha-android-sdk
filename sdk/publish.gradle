apply plugin: 'maven-publish'

ext {
    mGroupId = "com.ozanlimited"
    mArtifactId = "hcaptcha"
    mVersionCode = 1
    mVersionName = "1.0.0"

    mLibraryName = "Hcaptcha"
    mLibraryDescription = ""
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId mGroupId
                artifactId mArtifactId
                version mVersionName

                from components.release

                artifact androidSourcesJar

                pom {
                    name = mLibraryName
                    description = mLibraryDescription
                }
            }
        }

        repositories {
            maven {
                url System.getenv("GITLAB_REPO_URL")
                credentials(HttpHeaderCredentials) {
                    name = System.getenv("GITLAB_DEPLOY_KEY")
                    value = System.getenv("GITLAB_DEPLOY_TOKEN")
                }
                authentication {
                    header(HttpHeaderAuthentication)
                }
            }
        }
    }

    publish.dependsOn assemble
}
